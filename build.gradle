buildscript {
    repositories {
		maven {
            url "https://na.artifactory.swg-devops.com/artifactory/wh-imaging-external-maven-virtual"
            credentials {
                username = "${taasArtifactoryUsername}"
                password = "${taasArtifactoryPassword}"
            }
            metadataSources {
                mavenPom()
                artifact()
            }
        }
	}
}

plugins {
    id "ru.vyarus.use-python" version "2.2.0"
    id "org.sonarqube" version "3.0"
}

group = 'com.ibm.watson.health.imaging'
version = "3.2.0"
description = """CAF Python Logger"""

sonarqube {
    properties {
        property "sonar.sources", pySrcDir
        property "sonar.tests", pyTestDir
        property "sonar.python.coverage.reportPaths", pyXmlCoveragePath
        property "sonar.python.xunit.reportPath", pyTestReportPath
    }
}

// --------
//Whi CAF Python Build Section
python {
    //Python dependencies start
    pip 'contextvars:2.4;python_version<"3.7"'
    pip 'python-json-logger:0.1.11'
    pip 'aiohttp:3.7.3'
    //Python dependencies end

    envPath = '/opt/ibm/whi/app'
}


task whiCafPySetupBuild(type: PythonTask) {
    module = 'pip'
    extraArgs = ['install', 'pytest==4.6.3', 'pytest-cov==2.7.1', 'twine==1.14.0', 'flake8==3.7.8', 'asynctest==0.13.0']
}

task whiCafPyGenerateReqs {
    doLast {
        def newFile = new File("${project.projectDir}/pinned.txt")
        newFile.delete()
        newFile.createNewFile()
        python.modules.each { module ->
            newFile.append(module.replace(":", "==") + "\n")
        }
    }
}

task whiCafPySetupBuildProperties {
    doLast {
        def newFile = new File("${project.projectDir}/setup.properties")
        newFile.delete()
        newFile.createNewFile()
        newFile.append("[default]")
        newFile.append("\nproject_name=${project.name}")
        newFile.append("\nproject_version=${project.version}")
        newFile.append("\nproject_srcDir=${pySrcDir}")
    }
}

task whiCafPyBuildWheel(type: PythonTask) {
    command = 'setup.py'
    extraArgs = ['bdist_wheel', '-d', pyDistDir, '-k']
}

task whiCafPyTestPrep(type: PythonTask) {
    command = 'setup.py'
    extraArgs = ['develop']
}

task whiCafPyTest(type: PythonTask) {
    module = 'pytest' 
    extraArgs = [pyTestDir, '--junitxml='+pyTestReportPath, '-v']
}

task whiCafPyCoverage(type: PythonTask) {
    module = 'pytest' 
    extraArgs = [pyTestDir, '--cov', pySrcDir, '--cov-report=xml:'+pyXmlCoveragePath, '--cov-report=html:'+pyHtmlCoveragePath, '--cov-report=term']
}

task whiCafClean {
    doLast {
        exec {
            commandLine 'rm', '-rf', 'build'
        }
    }
}

task whiCafPyPublishArtifacts(type: PythonTask) {
    doFirst {
        def taasArtifactoryUsername = "${taasArtifactoryUsername}"
        def taasArtifactoryPassword = "${taasArtifactoryPassword}"
        module = 'twine'
        extraArgs = ['upload', '--repository-url', whiPypiRepositoryUrl, '-u', taasArtifactoryUsername, '-p', taasArtifactoryPassword, pyDistDir+'/*.whl'] 
    }
}

task whiCafPyFlake8(type: PythonTask) {
    module = 'flake8'
    extraArgs = [pySrcDir]
}

task build {}
task test {}
task coverage {}
task uploadArchives {}
task flake8 {}
task clean {}

project.tasks.whiCafPyTest.dependsOn project.tasks.whiCafPySetupBuild
project.tasks.whiCafPyTest.dependsOn project.tasks.whiCafPyTestPrep
project.tasks.whiCafPyTestPrep.dependsOn project.tasks.whiCafPySetupBuildProperties
project.tasks.whiCafPyCoverage.dependsOn project.tasks.whiCafPySetupBuild
project.tasks.whiCafPyCoverage.dependsOn project.tasks.whiCafPyTestPrep
project.tasks.whiCafPyPublishArtifacts.dependsOn project.tasks.whiCafPySetupBuild
project.tasks.whiCafPyFlake8.dependsOn project.tasks.whiCafPySetupBuild

project.tasks.build.dependsOn project.tasks.whiCafPySetupBuild
project.tasks.build.dependsOn project.tasks.whiCafPySetupBuildProperties
project.tasks.build.dependsOn project.tasks.whiCafPyGenerateReqs
project.tasks.build.dependsOn project.tasks.whiCafPyTest
project.tasks.build.finalizedBy project.tasks.whiCafPyBuildWheel

project.tasks.test.finalizedBy project.tasks.whiCafPyTest
project.tasks.coverage.finalizedBy project.tasks.whiCafPyCoverage
project.tasks.clean.finalizedBy project.tasks.whiCafClean
project.tasks.uploadArchives.finalizedBy project.tasks.whiCafPyPublishArtifacts
project.tasks.flake8.finalizedBy project.tasks.whiCafPyFlake8

// -----------



